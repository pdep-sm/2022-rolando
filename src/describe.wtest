import personaje.*
import artefactos.*
import hechiceria.*

const espectroMalefico = new HechizoDeLogos(nombre="espectro maléfico")
const rolando = new Personaje(hechizoPreferido = espectroMalefico)
const mascaraOscura = new MascaraOscura(indiceOscuridad = 1)
const armadura = new Armadura()
const espadaDelDestino = armaBlanca

describe "Tests de Hechiceria" {	
	
	const hechizoPoderoso = object {
			method esPoderoso() = true
		}

	test "Nivel de hechicería de Rolando - Base" {
		/**
		 * 56 = 3 * 17 (que es el poder de su hechizo preferido) + 5 (valor de la fuerza oscura)
		 */
		assert.equals(56, rolando.nivelDeHechiceria())
	}
	
	test "Nivel de hechicería de Rolando con espectro superrecontramalefico muajajaja" {
		const otroHechizoMuajaja = new HechizoDeLogos(nombre="espectro superrecontramalefico muajajaja")
		rolando.hechizoPreferido(otroHechizoMuajaja)
		assert.equals(125, rolando.nivelDeHechiceria())
	}
	
	test "Nivel de hechicería de Rolando con Hechizo básico" {
		rolando.hechizoPreferido(hechizoBasico)
		assert.equals(35, rolando.nivelDeHechiceria())
	}
	
	test "Nivel de hechicería de Rolando tras un eclipse" {
		fuerzaOscura.eclipse()
		assert.equals(61, rolando.nivelDeHechiceria())
	}
	
	test "Espectro maléfico es poderoso" {
		assert.that(espectroMalefico.esPoderoso())
	}
	
	test "Espectro maléfico 'pepe' NO es poderoso" {
		const hechizoPepe = new HechizoDeLogos(nombre = "pepe")
		assert.notThat(hechizoPepe.esPoderoso())
	}
	
	test "Espectro maléfico con 15 letras NO es poderoso" {
		const hechizoDeLogos = new HechizoDeLogos(nombre = "123456789012345")
		assert.notThat(hechizoDeLogos.esPoderoso())
	}
	
	test "Hechizo básico NO es poderoso" {
		assert.notThat(hechizoBasico.esPoderoso())
	}
	
	test "Rolando poderoso con hechizo poderoso" {
		rolando.hechizoPreferido(hechizoPoderoso)
		assert.that(rolando.esPoderoso())
	}
	
}


describe "Tests de Lucha" {
	
	const motosierraDelDestino = object {
		method unidadesDeLucha(personaje) = 1000
	}
		
	method initialize(){
		[armaBlanca, collarDivino, mascaraOscura].forEach{ artefacto => 
			rolando.agregarArtefacto(artefacto)
		}
		//rolando.agregarArtefacto(armaBlanca)
		//rolando.agregarArtefacto(collarDivino)
		//rolando.agregarArtefacto(mascaraOscura)
	}
	
	test "El valor de lucha de Rolando sin artefactos es 1" {
		rolando.artefactos().clear()
		assert.equals(1, rolando.valorDeLucha())
	}
	
	test "El valor de lucha de Rolando con sus artefactos base es de 13" {
		assert.equals(13, rolando.valorDeLucha())
	}
	
	test "El valor de lucha de Rolando con collar de 3 perlas es de 11" {
		collarDivino.cantidadDePerlas(3)
		assert.equals(11, rolando.valorDeLucha())
	}
	
	test "El valor de lucha de Rolando con valor base 8 asciende a 20" {
		rolando.valorBaseLucha(8)
		assert.equals(20, rolando.valorDeLucha())
	}
	
	test "El valor de lucha de Rolando con 'eclipse' es de 14" {
		fuerzaOscura.eclipse()
		assert.equals(14, rolando.valorDeLucha())
	}
	
	test "El valor de lucha de Rolando sin su Máscara Oscura es 9" {
		rolando.quitarArtefacto(mascaraOscura)
		assert.equals(9, rolando.valorDeLucha())
	}
	
	test "Rolando no es mejor luchador que hechicero (56 >> 13)" {
		assert.notThat(rolando.esMejorLuchadorQueHechicero())
	}
	
	test "Rolando es mejor luchador que hechicero (56 << 1000 aprox)" {
		rolando.agregarArtefacto(motosierraDelDestino)
		assert.that(rolando.esMejorLuchadorQueHechicero())
	}
	
	test "El valor de lucha de Rolando con Armadura sin refuerzo es 15" {
		rolando.agregarArtefacto(armadura)
		assert.equals(15, rolando.valorDeLucha())
	}

	test "El valor de lucha de Rolando con Armadura con bendición es 71" {
		rolando.agregarArtefacto(armadura)
		armadura.refuerzo(bendicion)
		assert.equals(71, rolando.valorDeLucha())
	}	
	
}